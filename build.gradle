group 'com.github.complate'
version '0.1.1'

buildscript {
  ext.kotlin_version = '1.1.51'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'application'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

  // HTTP4K
  compile group: "org.http4k", name: "http4k-core", version: "2.35.1"
  compile group: "org.http4k", name: "http4k-server-netty", version: "2.35.1"

  // Complate
  compile 'com.github.complate:complate-http4k:0.1.1'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

mainClassName = "com.github.complate.sample.http4k.BootstrapKt"

task npmInstall(type: Exec) {
  description = "Grab NodeJS dependencies (from package.json)"
  commandLine = ["npm", "install"]
  inputs.file "package.json"

  doLast {
    new ByteArrayOutputStream().withStream { os ->
      exec {
        executable = 'npm'
        args = ['bin']
        standardOutput = os
      }
      ext.binPath = os.toString().trim() + "/"
    }
  }
}
